#!/usr/bin/perl
# ------------------------------------------------------------------------------
# (C) British Crown Copyright 2006-17 Met Office.
#
# This file is part of FCM, tools for managing and building source code.
#
# FCM is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# FCM is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with FCM. If not, see <http://www.gnu.org/licenses/>.
# ------------------------------------------------------------------------------

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../lib";
use FCM::Admin::System qw{
    filter_projects
    get_projects_from_svn_backup
    recover_svn_repository
};
use FCM::Admin::Util qw{option2config};
use Getopt::Long qw{GetOptions};
use Pod::Usage qw{pod2usage};

main();

sub main {
    my %option;
    my $result = GetOptions(
        \%option,
        q{help|usage|h},
        q{no-recover-dumps},
        q{no-recover-hooks},
        q{svn-backup-dir=s},
        q{svn-dump-dir=s},
        q{svn-live-dir=s},
        q{svn-project-suffix=s},
    );
    if (!$result) {
        pod2usage(1);
    }
    if (exists($option{help})) {
        pod2usage(q{-verbose} => 1);
    }
    option2config(\%option);
    my @projects = filter_projects([get_projects_from_svn_backup()], \@ARGV);
    for my $project (sort {$a->get_name() cmp $b->get_name()} @projects) {
        recover_svn_repository(
            $project,
            !$option{q{no-recover-dumps}},
            !$option{q{no-recover-hooks}},
        );
    }
}

__END__

=head1 NAME

fcm-recover-svn-repos

=head1 SYNOPSIS

    fcm-recover-svn-repos [OPTIONS] [PROJECT ...]

=head1 OPTIONS

=over 4

=item --help, -h, --usage

Prints help and exits.

=item --no-recover-dumps

If this option is specified, the program will not attempt to load the revision
dumps of each repository after it has been restored from the backup.

=item --no-recover-hooks

If this option is specified, the program will not attempt to reinstate the hook
scripts for each repository after it has been restored from the backup.

=item --svn-backup-dir=DIR

Specifies the root location of the backup directory. See
L<FCM::Admin::Config|FCM::Admin::Config> for the current default.

=item --svn-dump-dir=DIR

Specifies the root location of the directory where revision dumps are kept. See
L<FCM::Admin::Config|FCM::Admin::Config> for the current default.

=item --svn-live-dir=DIR

Specifies the root location of the live directory. See
L<FCM::Admin::Config|FCM::Admin::Config> for the current default.

=item --svn-project-suffix=NAME

Specifies the suffix added to the project name. See
L<FCM::Admin::Config|FCM::Admin::Config> for the current default.

=back

=head1 ARGUMENTS

=over 4

=item PROJECT

Specifies one or more project to recover. If no project is specified, the
program searches the backup directory for projects to recover.

=back

=head1 DESCRIPTION

This program archives Subversion repositories in the live directory to the
backup directory.

=head1 COPYRIGHT

E<169> Crown copyright Met Office. All rights reserved.

=cut
